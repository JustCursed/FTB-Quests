buildscript {
	repositories {
		jcenter()
		maven { url "https://plugins.gradle.org/m2/" }
		maven {
			name = 'forge'
			url = 'https://maven.minecraftforge.net/'
		}
	}
	dependencies {
		classpath('com.anatawa12.forge:ForgeGradle:2.3-1.0.+') {
			changing = true
		}
		classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.1.0'
		classpath 'com.wynprice.cursemaven:CurseMaven:1.2.+'
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'
apply plugin: 'maven'

apply from: 'https://raw.githubusercontent.com/MinecraftModDevelopment/Gradle-Collection/master/generic/markdown-git-changelog.gradle'

def ENV = System.getenv()
version = ENV.BUILD_NUMBER ? "${mod_version}.${ENV.BUILD_NUMBER}" : "${mod_version}"
group = package_group
archivesBaseName = mod_name

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
	sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
	version = '1.12.2-14.23.5.2838'
	runDir = "run"
	mappings = project.mappings_version

	replace "0.0.0.ftbquests", project.version
	replaceIn project.mod_reference
}

configurations.all {
	transitive = false
}

repositories {
	mavenLocal()
}

dependencies {
	implementation fileTree(dir: project.lib, include: ['*-deobf.jar'], exclude: [])
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version': project.version, 'mcversion': project.minecraft.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

artifacts {
	archives sourceJar
}

if (ENV.LOCAL_MAVEN) {
	uploadArchives {
		repositories {
			mavenDeployer {
				repository(url: "file://${ENV.LOCAL_MAVEN}")
			}
		}
	}
}

if (ENV.CURSEFORGE_KEY) {
	curseforge {
		apiKey = ENV.CURSEFORGE_KEY
		project {
			id = project.curseforge_id
			releaseType = "release"
			addGameVersion project.mc_version
			relations {
				requiredDependency 'ftb-library'
				requiredDependency 'item-filters'
				optionalDependency 'kubejs'
				optionalDependency 'ftb-utilities'
				optionalDependency 'jei'
				optionalDependency 'industrial-craft'
				optionalDependency 'packmode'
				optionalDependency 'botania'
				optionalDependency 'baubles'
				optionalDependency 'buildcraft-core'
				optionalDependency 'projecte'
				optionalDependency 'game-stages'
				optionalDependency 'custom-npcs'
				optionalDependency 'reskillable'
			}
			changelog = getGitChangelog
			changelogType = 'markdown'
			addArtifact(sourceJar)
		}
	}
}